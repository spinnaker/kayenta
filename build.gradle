/*
 * Copyright 2017 Google, Inc.
 * Copyright 2017 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    jcenter()
    maven { url "http://spinnaker.bintray.com/gradle" }
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath "com.netflix.spinnaker.gradle:spinnaker-gradle-project:3.17.0"
    classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    classpath "com.netflix.nebula:nebula-kotlin-plugin:$kotlinVersion"
  }
}

allprojects {
  group = "com.netflix.kayenta"
  apply plugin: "groovy"
  apply plugin: "spinnaker.project"

  test {
    testLogging {
      exceptionFormat = "full"
    }
    minHeapSize = "512m"
    maxHeapSize = "512m"
  }
  tasks.withType(JavaExec) {
    if (System.getProperty('DEBUG', 'false') == 'true') {
      jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=8191'
    }
  }
}

subprojects { project ->
  repositories {
    jcenter()
    maven { url "https://dl.bintray.com/spinnaker/spinnaker"}
  }

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  dependencies {
    // spinnaker.group('test')
    testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testCompile "org.spockframework:spock-core:1.1-groovy-2.4-rc-2"
    testCompile "org.spockframework:spock-spring:1.1-groovy-2.4-rc-2"
    testCompile "org.springframework:spring-test:$springVersion"
    testCompile "cglib:cglib-nodep:3.2.0"
    testCompile "org.objenesis:objenesis:2.5.1"
    testCompile "junit:junit:4.12"
    testCompile "org.hamcrest:hamcrest-core:1.3"
  }
}

defaultTasks ":kayenta-web:bootRun"
